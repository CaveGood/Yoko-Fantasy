extends "res://entities/units/player/player.gd"

var decaying_slow_enemy_when_below_hp_triggers: Dictionary = {}
var _fantasy_active_decaying_slow_sources: Dictionary = {}
var _fantasy_decaying_slow_source_enemies: Dictionary = {}
var _fantasy_decaying_slow_original_materials: Dictionary = {}
var _fantasy_decaying_slow_enemy_counts: Dictionary = {}
var consumables_picked_up_this_run: Dictionary = {}
var _fantasy_clock_tower_hat_sprite: Sprite = null
var _fantasy_clock_tower_hat_tween: Tween = null
var _fantasy_clock_tower_hat_material: ShaderMaterial = null
var _fantasy_clock_tower_hat_in_area: bool = false
var _fantasy_is_clock_tower_guardian_character: bool = false
var _fantasy_active_soul_effects: Array = []

const FANTASY_CLOCK_TOWER_HAT_PATH: String = "res://mods-unpacked/Yoko-Fantasy/content/characters/clock_tower_guardian/clock_tower_guardian_hat.webp"
const FANTASY_CLOCK_TOWER_HAT_TWEEN_TIME: float = 0.35
const FANTASY_CLOCK_TOWER_HAT_IN_AREA_EFFECT: float = 1.0

var _fa_healing_star_receiver = null

# ══════════════════════════════════════════ Extension ══════════════════════════════════════════ #
func _ready() -> void:
    _fantasy_decaying_slow_enemy_when_below_hp_ready()
    _fantasy_is_clock_tower_guardian_character = _fantasy_is_clock_tower_guardian()

func _physics_process(_delta: float) -> void:
    _fantasy_update_clock_tower_hat_color()

func get_damage_value(dmg_value: int, _from_player_index: int, armor_applied := true, dodgeable := true, _is_crit := false, _hitbox: Hitbox = null, _is_burning := false) -> Unit.GetDamageValueResult:
    var result: Unit.GetDamageValueResult =.get_damage_value(dmg_value, _from_player_index, armor_applied, dodgeable, _is_crit, _hitbox, _is_burning)
    result = _fantasy_damage_clamp(result)

    return result

func _on_LoseHealthTimer_timeout() -> void:
    if _fantasy_lose_hp_per_second_min_hp(): return
    if _fantasy_lose_hp_per_second_stop_threshold(): return
    ._on_LoseHealthTimer_timeout()

func take_damage(value: int, args: TakeDamageArgs) -> Array:
    var take_damage_array: Array =.take_damage(value, args)
    _fantasy_damage_reflect(take_damage_array[0], args)
    _fantasy_decaying_slow_enemy_when_below_hp(take_damage_array[1])
    _fantasy_loss_material_on_hit(take_damage_array[1])

    return take_damage_array

func _on_ItemAttractArea_area_entered(item: Item) -> void:
    ._on_ItemAttractArea_area_entered(item)
    _fantasy_on_soul_entered(item)

func on_consumable_picked_up(consumable_data: ConsumableData) -> void:
    .on_consumable_picked_up(consumable_data)
    _fantasy_dmg_when_pickup_consumable(consumable_data)
    _fantasy_add_stat_when_pickup_consumable(consumable_data)

func fa_begin_healing_star_collection(receiver) -> void:
    _fa_healing_star_receiver = receiver

func fa_end_healing_star_collection(receiver) -> void:
    if _fa_healing_star_receiver == receiver:
        _fa_healing_star_receiver = null

func on_healing_effect(value: int, tracking_key: int = Keys.empty_hash, from_torture: bool = false) -> int:
    if is_instance_valid(_fa_healing_star_receiver):
        _fa_healing_star_receiver.fa_store_healing(value)
        return 0

    return .on_healing_effect(value, tracking_key, from_torture)

func on_heal_over_time_effect(total_healing: int, duration: int) -> void:
    if is_instance_valid(_fa_healing_star_receiver):
        _fa_healing_star_receiver.fa_store_healing(total_healing)
        return

    .on_heal_over_time_effect(total_healing, duration)

func die(args = Utils.default_die_args) -> void:
    _fantasy_clear_decaying_slow_effects()
    _fantasy_clear_soul_effects()
    .die(args)

func on_room_cleanup() -> void:
    _fantasy_clear_decaying_slow_effects()
    _fantasy_clear_soul_effects()
    .on_room_cleanup()

# ══════════════════════════════════════════ Custom ══════════════════════════════════════════ #
func _fantasy_damage_clamp(result: Unit.GetDamageValueResult) -> Unit.GetDamageValueResult:
    var effect_items: Array = RunData.get_player_effect(Utils.fantasy_damage_clamp_hash, player_index)
    for effect in effect_items:
        var max_hp: float = Utils.get_stat(Keys.stat_max_hp_hash, player_index)
        var tracking_key_hash: int = effect[0]
        var max_percent: float = effect[2] / 100.0
        var max_allowed_dmg: int = max(1, int(max_hp * max_percent))
        var max_taken_dmg: int = int(clamp(result.value, min(effect[1], result.value), max_allowed_dmg))

        RunData.ncl_add_effect_tracking_value(tracking_key_hash, result.value - max_taken_dmg, player_index)
        result.value = max_taken_dmg

    return result

func _fantasy_update_clock_tower_hat_color() -> void:
    if !_fantasy_is_clock_tower_guardian_character:
        return

    var has_clock_tower_area: bool = Utils.fa_has_clock_tower_area(player_index)
    if !has_clock_tower_area:
        if _fantasy_clock_tower_hat_in_area:
            _fantasy_set_clock_tower_hat_in_area(false)
        return

    var in_area: bool = Utils.fa_is_clock_tower_player_in_area(player_index)
    if in_area == _fantasy_clock_tower_hat_in_area:
        return

    _fantasy_set_clock_tower_hat_in_area(in_area)

func _fantasy_is_clock_tower_guardian() -> bool:
    var character_data: CharacterData = RunData.get_player_character(player_index)
    return character_data != null and character_data.my_id_hash == Utils.character_fantasy_clock_tower_guardian_hash

func _fantasy_set_clock_tower_hat_in_area(in_area: bool) -> void:
    var hat_sprite: Sprite = _fantasy_get_clock_tower_hat_sprite()
    if hat_sprite == null:
        return

    _fantasy_clock_tower_hat_in_area = in_area
    var target_effect: float = FANTASY_CLOCK_TOWER_HAT_IN_AREA_EFFECT if in_area else 0.0
    _fantasy_tween_clock_tower_hat(target_effect)

func _fantasy_get_clock_tower_hat_sprite() -> Sprite:
    if is_instance_valid(_fantasy_clock_tower_hat_sprite):
        return _fantasy_clock_tower_hat_sprite

    for appearance_sprite in _item_appearances:
        if !(appearance_sprite is Sprite):
            continue
        var sprite: Sprite = appearance_sprite
        if sprite.texture == null:
            continue
        if sprite.texture.resource_path != FANTASY_CLOCK_TOWER_HAT_PATH:
            continue

        _fantasy_clock_tower_hat_sprite = sprite
        _fantasy_prepare_clock_tower_hat_material(sprite)
        return _fantasy_clock_tower_hat_sprite

    return null

func _fantasy_prepare_clock_tower_hat_material(hat_sprite: Sprite) -> void:
    if is_instance_valid(_fantasy_clock_tower_hat_material):
        return

    var shader: Shader = Shader.new()
    shader.code = "shader_type canvas_item;\n" \
        + "uniform float effect_amount = 0.0;\n" \
        + "void fragment() {\n" \
        + "\tvec4 tex = texture(TEXTURE, UV);\n" \
        + "\tfloat luma = dot(tex.rgb, vec3(0.299, 0.587, 0.114));\n" \
        + "\tvec3 shadow_blue = vec3(0.12, 0.20, 0.34);\n" \
        + "\tvec3 brass_gold = vec3(1.0, 0.76, 0.26);\n" \
        + "\tvec3 contrast = mix(shadow_blue, brass_gold, smoothstep(0.14, 0.92, luma));\n" \
        + "\tvec3 color_shift = mix(tex.rgb, contrast, 0.48);\n" \
        + "\tfloat scan = smoothstep(0.035, 0.0, abs(fract(UV.y * 2.45 - TIME * 0.55) - 0.5));\n" \
        + "\tcolor_shift += brass_gold * scan * 0.28 * tex.a;\n" \
        + "\tfloat pulse = 0.88 + 0.12 * sin(TIME * 3.4);\n" \
        + "\tcolor_shift *= pulse;\n" \
        + "\tCOLOR = vec4(mix(tex.rgb, color_shift, effect_amount), tex.a) * COLOR;\n" \
        + "}"

    _fantasy_clock_tower_hat_material = ShaderMaterial.new()
    _fantasy_clock_tower_hat_material.shader = shader
    _fantasy_clock_tower_hat_material.set_shader_param("effect_amount", 0.0)
    hat_sprite.material = _fantasy_clock_tower_hat_material

func _fantasy_tween_clock_tower_hat(target_effect: float) -> void:
    if !is_instance_valid(_fantasy_clock_tower_hat_material):
        return

    if !is_instance_valid(_fantasy_clock_tower_hat_tween):
        _fantasy_clock_tower_hat_tween = Tween.new()
        add_child(_fantasy_clock_tower_hat_tween)

    _fantasy_clock_tower_hat_tween.stop_all()
    _fantasy_clock_tower_hat_tween.remove_all()
    _fantasy_clock_tower_hat_tween.interpolate_property(
        _fantasy_clock_tower_hat_material,
        "shader_param/effect_amount",
        _fantasy_clock_tower_hat_material.get_shader_param("effect_amount"),
        target_effect,
        FANTASY_CLOCK_TOWER_HAT_TWEEN_TIME,
        Tween.TRANS_SINE,
        Tween.EASE_IN_OUT
    )
    _fantasy_clock_tower_hat_tween.start()

func _fantasy_damage_reflect(full_dmg_value: int, args: TakeDamageArgs) -> void:
    if !is_instance_valid(args.hitbox) or !is_instance_valid(args.hitbox.from): return

    if !(args.hitbox.from is Enemy): return

    var enemy: Enemy = args.hitbox.from
    var effect_items: Array = RunData.get_player_effect(Utils.fantasy_damage_reflect_hash, player_index)
    for effect_item in effect_items:
        var tracking_key_hash: int = effect_item[0]
        var reflect_percent: float = effect_item[1] / 100.0
        var reflect_args: TakeDamageArgs = TakeDamageArgs.new(player_index)
        var percent_damage_bonus: float = 1 + Utils.get_stat(Keys.stat_percent_damage_hash, player_index) / 100.0
        var reflect_damage: int = int(full_dmg_value * reflect_percent * percent_damage_bonus)

        RunData.add_tracked_value(player_index, tracking_key_hash, reflect_damage)
        enemy.take_damage(reflect_damage, reflect_args)

func _fantasy_decaying_slow_enemy_when_below_hp_ready() -> void:
    var effect_items: Array = RunData.get_player_effect(Utils.fantasy_decaying_slow_enemy_when_below_hp_hash, player_index)
    for effect_index in range(effect_items.size()):
        var effect: Array = effect_items[effect_index]
        decaying_slow_enemy_when_below_hp_triggers[effect_index] = effect[3] # Trigger times

func _fantasy_decaying_slow_enemy_when_below_hp(dmg_taken: int) -> void:
    if dmg_taken <= 0: return

    var effect_items: Array = RunData.get_player_effect(Utils.fantasy_decaying_slow_enemy_when_below_hp_hash, player_index)
    for effect_index in range(effect_items.size()):
        var effect: Array = effect_items[effect_index]
        var hp_threshold: float = max_stats.health * effect[0] / 100.0
        var duration: int = effect[1]
        var stat_nb: int = effect[2]

        if current_stats.health >= hp_threshold or decaying_slow_enemy_when_below_hp_triggers[effect_index] <= 0: continue

        decaying_slow_enemy_when_below_hp_triggers[effect_index] -= 1

        TempStats.add_stat(Utils.stat_fantasy_decaying_slow_enemy_hash, stat_nb, player_index) # For main.gd to use
        var source_id: String = _fantasy_get_decaying_slow_source_id(effect_index)
        _fantasy_active_decaying_slow_sources[source_id] = stat_nb
        _fantasy_decaying_slow_source_enemies[source_id] = []
        var enemies: Array = Utils.get_scene_node()._entity_spawner.get_all_enemies(false)
        for enemy in enemies:
            fa_apply_decaying_slow_to_enemy(enemy, source_id, stat_nb)

        yield (get_tree().create_timer(duration, false), "timeout")
        if cleaning_up:
            _fantasy_active_decaying_slow_sources.erase(source_id)
            fa_remove_decaying_slow_source(source_id)
            return

        TempStats.remove_stat(Utils.stat_fantasy_decaying_slow_enemy_hash, stat_nb, player_index)
        _fantasy_active_decaying_slow_sources.erase(source_id)
        fa_remove_decaying_slow_source(source_id)
        break # Once a time when take damage

func _fantasy_get_decaying_slow_source_id(effect_index: int) -> String:
    return "decaying_slow_%s_%s_%s" % [player_index, effect_index, decaying_slow_enemy_when_below_hp_triggers[effect_index]]

func fa_apply_decaying_slow_to_enemy(enemy: Enemy, source_id: String, slow_percent: float) -> void:
    if !is_instance_valid(enemy) or enemy.dead: return

    EnemySpeedModifierService.set_percent_modifier(enemy, source_id, slow_percent)
    if !_fantasy_decaying_slow_source_enemies.has(source_id):
        _fantasy_decaying_slow_source_enemies[source_id] = []
    if !_fantasy_decaying_slow_source_enemies[source_id].has(enemy):
        _fantasy_decaying_slow_source_enemies[source_id].append(enemy)

    if !_fantasy_decaying_slow_original_materials.has(enemy):
        match enemy.sprite.material == enemy.flash_mat:
            true: _fantasy_decaying_slow_original_materials[enemy] = enemy._non_flash_material
            false: _fantasy_decaying_slow_original_materials[enemy] = enemy.sprite.material
        _fantasy_decaying_slow_enemy_counts[enemy] = 0

    _fantasy_decaying_slow_enemy_counts[enemy] += 1
    enemy.sprite.material = load("res://mods-unpacked/Yoko-Fantasy/extensions/effects/decaying_slow_enemy_when_below_hp/decaying_slow_enemy_when_below_hp_shader.tres")

func fa_remove_decaying_slow_source(source_id: String) -> void:
    var enemies: Array = _fantasy_decaying_slow_source_enemies.get(source_id, [])
    for enemy in enemies:
        EnemySpeedModifierService.remove_modifier(enemy, source_id)
        if !_fantasy_decaying_slow_enemy_counts.has(enemy):
            continue

        _fantasy_decaying_slow_enemy_counts[enemy] -= 1
        if _fantasy_decaying_slow_enemy_counts[enemy] > 0:
            continue

        _fantasy_decaying_slow_enemy_counts.erase(enemy)
        if is_instance_valid(enemy) and _fantasy_decaying_slow_original_materials.has(enemy):
            enemy.sprite.material = _fantasy_decaying_slow_original_materials[enemy]
        _fantasy_decaying_slow_original_materials.erase(enemy)

    _fantasy_decaying_slow_source_enemies.erase(source_id)

func _fantasy_clear_decaying_slow_effects() -> void:
    for source_id in _fantasy_active_decaying_slow_sources.keys().duplicate():
        TempStats.remove_stat(Utils.stat_fantasy_decaying_slow_enemy_hash, _fantasy_active_decaying_slow_sources[source_id], player_index)
        _fantasy_active_decaying_slow_sources.erase(source_id)
        fa_remove_decaying_slow_source(source_id)

func _fantasy_loss_material_on_hit(dmg_taken: int) -> void:
    if dmg_taken <= 0: return

    var materials_to_remove: int = RunData.get_player_effect(Utils.fantasy_material_loss_on_hit_hash, player_index)
    if materials_to_remove <= 0: return

    RunData.remove_gold(materials_to_remove, player_index)
    RunData.emit_signal("stat_removed", Keys.stat_materials_hash, materials_to_remove, -15.0, player_index)

func _fantasy_dmg_when_pickup_consumable(consumable_data: ConsumableData) -> void:
    var effect_items: Array = RunData.get_player_effect(Utils.fantasy_dmg_when_pickup_consumable_hash, player_index)
    if effect_items.empty(): return

    for effect_item in effect_items:
        var consumable_id: int = effect_item[0]

        if consumable_data.my_id_hash != consumable_id: continue

        var enemies: Array = Utils.get_scene_node()._entity_spawner.get_all_enemies(false)

        if enemies.empty(): return

        enemies.shuffle()

        var max_num: int = effect_item[1]
        var scaling_stats: Array = effect_item[2]
        var base_damage: int = effect_item[3]
        var tracked_key: int = effect_item[4]
        var damage_color: Color = effect_item[5]
        var total_damage: int = Utils.ncl_get_dmg_with_scaling_stats(base_damage, scaling_stats, player_index)
        var damage_args: TakeDamageArgs = Utils.ncl_create_custom_damage_args(player_index, damage_color)

        var processed_count = 0
        for i in range(min(max_num, enemies.size())):
            var enemy: Enemy = enemies[i]

            if !is_instance_valid(enemy) or enemy.dead: continue

            var take_damage_array: Array = enemy.take_damage(total_damage, damage_args)
            RunData.add_tracked_value(player_index, tracked_key, take_damage_array[1])
            processed_count += 1

            if processed_count >= max_num: break

func _fantasy_lose_hp_per_second_min_hp() -> bool:
    var lose_hp_per_second_min_hp: int = RunData.get_player_effect(Utils.fantasy_lose_hp_per_second_min_hp_hash, player_index)
    if lose_hp_per_second_min_hp <= 0: return false

    _take_damage_args.dodgeable = false
    _take_damage_args.armor_applied = false
    _take_damage_args.bypass_invincibility = true
    _take_damage_args.from = self
    var lose_hp_per_second = RunData.get_player_effect(Keys.lose_hp_per_second_hash, player_index)
    if current_stats.health <= lose_hp_per_second + lose_hp_per_second_min_hp: lose_hp_per_second = current_stats.health - lose_hp_per_second_min_hp

    if lose_hp_per_second > 0: var _dmg_taken: Array = take_damage(lose_hp_per_second, _take_damage_args)
    elif lose_hp_per_second == 0: pass
    else: var _healed: int = on_healing_effect(-lose_hp_per_second)
    
    return true

func _fantasy_on_soul_entered(item: Item) -> void:
    if !(item is Consumable): return

    var consumable_data: ConsumableData = item.consumable_data
    if consumable_data.my_id_hash != Utils.consumable_fantasy_soul_hash: return

    if item.attracted_by == null:
        item.attracted_by = self
        item.set_physics_process(true)

func _fantasy_add_stat_when_pickup_consumable(consumable_data: ConsumableData) -> void:
    var effect_items: Array = RunData.get_player_effect(Utils.fantasy_add_stat_when_pickup_consumable_hash, player_index)
    if effect_items.empty(): return

    for effect_item in effect_items:
        var consumable_id: int = effect_item[0]

        if consumable_data.my_id_hash != consumable_id: continue

        consumables_picked_up_this_run[consumable_id] = consumables_picked_up_this_run.get(consumable_id, 0) + 1
        var need_consumable_nb: int = effect_item[1]
        if consumables_picked_up_this_run[consumable_id] % need_consumable_nb != 0: continue

        var stat: int = effect_item[2]
        var stat_nb: int = effect_item[3]
        RunData.add_stat(stat, stat_nb, player_index)

func _fantasy_lose_hp_per_second_stop_threshold() -> bool:
    var lose_hp_per_second_stop_threshold: int = RunData.get_player_effect(Utils.fantasy_lose_hp_per_second_stop_threshold_hash, player_index)
    if lose_hp_per_second_stop_threshold <= 0: return false

    var current_hp_percent: int = int(float(current_stats.health) / float(max_stats.health) * 100.0)
    if current_hp_percent >= lose_hp_per_second_stop_threshold: return false

    return true

# ══════════════════════════════════════════ Method ══════════════════════════════════════════ #
func fa_on_soul_effect(damage_to_add: int, speed_to_add: int) -> void:
    var soul_effect: Dictionary = {
        "damage": damage_to_add,
        "speed": speed_to_add,
    }
    _fantasy_active_soul_effects.append(soul_effect)
    var timer: SceneTreeTimer = get_tree().create_timer(2.0, false)
    var _e: int = timer.connect("timeout", self , "fa_on_soul_effect_timer_timeout", [soul_effect])

func fa_on_soul_effect_timer_timeout(soul_effect: Dictionary) -> void:
    if !_fantasy_active_soul_effects.has(soul_effect): return

    _fantasy_remove_soul_effect(soul_effect)

func _fantasy_remove_soul_effect(soul_effect: Dictionary) -> void:
    var damage_to_remove: int = soul_effect.damage
    var speed_to_remove: int = soul_effect.speed
    _fantasy_active_soul_effects.erase(soul_effect)
    Utils.ncl_quiet_add_stat(Utils.stat_fantasy_soul_hash, -1, player_index)
    TempStats.remove_stat(Keys.stat_percent_damage_hash, damage_to_remove, player_index)
    TempStats.remove_stat(Keys.stat_attack_speed_hash, speed_to_remove, player_index)

func _fantasy_clear_soul_effects() -> void:
    for soul_effect in _fantasy_active_soul_effects.duplicate():
        _fantasy_remove_soul_effect(soul_effect)
